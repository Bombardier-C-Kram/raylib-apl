:Namespace setup
⍝ Makes an importer that improves the ffi function creation syntax

⎕io←1

debug ← {
  0:_←⍬
  ⎕←'StackSize',topRuntime
  ⎕←⍵
  ⍵
}

Assert ← {⍺←'assertion failure' ⋄ 0∊⍵:⍺{(⎕←⍺) ⎕SIGNAL 8}⍵ ⋄ shy←0}

⍝ predefined C types
atomTypes ← ⍬
atomTypes ,←⊂ bool ← 'I1'
atomTypes ,←⊂ c    ← 'I1'
atomTypes ,←⊂ i    ← 'I4'
atomTypes ,←⊂ f    ← 'F4'
atomTypes ,←⊂ u    ← 'U4'
atomTypes ,←⊂ i1   ← 'I1'
atomTypes ,←⊂ u1   ← 'U1'
atomTypes ,←⊂ u8   ← 'U8'
atomTypes ,←⊂ ptr  ← ,'P'
atomTypes ,←⊂ f8   ← 'F8'
atomTypes ,←⊂ i8   ← 'I8'
atomTypes ,←⊂ u2   ← 'U2'
atomTypes←∪atomTypes

lib ← '../libtemp-c-raylib.so'
unitPopGen ← {
  _←'CpyPop'⎕NA lib,'|memcpy2 >',⍵,' P U8'
  o ← CpyPop{ ⍺⍺ (⍬ (Pop⍬) ⍵⍵) }⍺
  ⎕OR'o'
}
unitPushGen ← {
  _←'CpyPush'⎕NA lib,'|memcpy2 P <',⍵,' U8'
  o ← CpyPush{ ptr⊣⍺⍺((ptr←Push⍬)(,⊂⍵) ⍵⍵) }⍺
  ⎕OR'o'
}
typeByteSize ← {⍵≡,'P':8 ⋄ ⍎⊃⌽⍵}¨atomTypes
pop  ← typeByteSize unitPopGen ¨ atomTypes
push ← typeByteSize unitPushGen¨ atomTypes

structs ← ∪{⍵/⍨'⋄'∘≡¨⊃∘⊃¨⍵}##.(⍎¨⎕NL) ¯2
'No structs found' Assert 0≢≢structs
⎕NA 'I4 ',lib,'|GetStructSize I4'
'tempAlloc' ⎕NA 'P ',lib,'|MemAlloc U4'
'tempFree'  ⎕NA      lib,'|MemFree P'
BytesEachArg ← ⌈/ GetStructSize¨ ⎕IO-⍨⍳≢structs
atomTypes ,← 1↓¨⊃¨structs

_F ← {f←⍺⍺ ⋄ ⎕OR'f'}
MapFuncs ← {⍺≡⊃⍺:⍺∘⊢⍵ ⋄ ⍺∇¨⍵}

PtrToBytes ← {
  _←'fromU1'⎕NA lib,'|memcpy2 =U1[','] P U8',⍨⍕⍵
  fromU1(⍵⍴0)⍺⍵
}

⍝ ---------------- ⍝
⍝ Stack operations ⍝
⍝ ---------------- ⍝
stack ← ⍬
topRuntime ← 0

Push ← {
  topRuntime+←1
  stack ,← {0≡⍵:0⋄tempAlloc BytesEachArg}¨⍳0⌈topRuntime-≢stack ⍝ Make sure stack always has enough space
  topRuntime⊃stack
}
Push⍬ ⍝ TODO: This fixes a stack underflow bug in the 3d-duck example and I sadly currently don't know why

Pop ← {
  topRuntime-←1
  _←'Stack underflow' Assert 0≤topRuntime
  (topRuntime+1)⊃stack
}

NameOfStruct ← {
  2>|≡⍵:⍵
  _← 'NameCheck applied to a non-atomic pointer type, this should be impossible.' Assert ∧/'<=>'≢¨⊂⊃⍵
  '⋄'∘,⍣¯1⊃⍵
}
remPtrInfo ← {⍵/⍨~ (~atomTypes∊⍨⊂⍵)∧ ∧\'<=>'∊⍨⍵}

F5←{
  name←NameOfStruct ⍵
  cleaned ← {0=≢⍵: ⍵ ⋄ NameOfStruct¨⍵} {'='≡⊃⍵:,'P'⋄⍵}¨ 1↓⍵
  _←('Struct ',name,' not cleaned properly') Assert ∧/2>≡¨cleaned
  args←' P'⍴⍨2×1+≢cleaned   ⍝ Includes result
  _←'IF'⎕NA¨(lib,'|',name,,∘args)¨'INTO' 'FROM'

  O←F{ ⍝ pop value
    i←atomTypes⍳⍵⍵
    _←⍺⍺ (Push¨i),⍨Pop⍬
    ⌽{⍵∘⊢⍬}¨⌽pop[i]
  }cleaned
  U←I{ ⍝ push value
    (name cleaned)←⍵⍵
    i←atomTypes⍳cleaned
    i≢⍥≢⍵:0 Assert⍨name,' (≢⍵)≢',⍕≢i
    _←push[i] {⍺∘⊢⍵}¨⍵
    _←⍺⍺ (ptr←Push⍬),⌽Pop¨i
    ptr
  }(name cleaned)

  ⎕OR¨'OU'
}
({pop,←1⊃⍵}⊣{push,←2⊃⍵}) ↓⍉↑F5¨structs

⍝ --------------------------- ⍝
⍝ Handling all =type versions ⍝
⍝ --------------------------- ⍝
equalPtrPopGen←{
  O←⍵{ ⍺⍺ ⍬⊣⍵⍵⍬ } (pop[atomTypes⍳⊂,'P'])
  ⎕OR'O'
}
equalPtrPushGen←{
  O←⍵{
    _←'Internal error: Tried pushing more or less than 1 amount of reference pointer' Assert 1≡≢⍵
    ⍵⍵ ⍺⍺⊃⍵
  }(⊃push[atomTypes⍳⊂,'P'])
  ⎕OR'O'
}
pop ,← equalPtrPopGen ¨ pop
push,← equalPtrPushGen¨ push
atomTypes,←'=',¨atomTypes
⍝ --------------------------- ⍝


⍝ --------------------------- ⍝
⍝ Handling signed string type ⍝
⍝ --------------------------- ⍝
atomTypes ,←⊂ str ← '=0C1'
StringPopGen←{
  _←'F'⎕NA lib,'|memcpy2 >P P U8'
  _←'A'⎕NA lib,'|strcpy2 >0C1 P'
  pop←F{
    ptr←Pop⍬
    _←strPtr ← ⍺⍺ (⍬ ptr 8)
    r←⍵⍵ (⍬ ptr)
    _←tempFree strPtr
    r
  }A
  ⎕OR 'pop'
}
StringPushGen←{
  _←'I'⎕NA lib,'|memcpy2 P <P U8'
  _←'M'⎕NA lib,'|strcpy2 P <0C1'
  push←I{
    ptr←Push⍬
    (⍕≢⊢)⍵:0 Assert⍨'Expected string, got: ',⍎⍵
    strPtr ← tempAlloc 1+≢⍵
    _←⍵⍵ (strPtr ⍵)
    _←⍺⍺{⍺⍺ ⍵} (ptr strPtr 8)
    ptr
  }M
  ⎕OR 'push'
}
pop ,←⊂ StringPopGen  ⍬
push,←⊂ StringPushGen ⍬
⍝ --------------------------- ⍝


∇F←R (name t);bm;in;out;io
  'Func'⎕NA lib,'|',name,' P'⍴⍨2×≢t
  io←(~atomTypes∊⍨t)∧'='≡¨⊃¨t ⍝ In and out
  bm ← '>'≢¨⊃¨t ⍝ In or out mask, neither is impossible
  in  ← {(~atomTypes∊⍨⊂⍵)∧'='≡⊃⍵: ,'P' ⋄ ⍵}¨ {0=≢⍵: ⍵ ⋄ (b/¨⍵),¨ NameOfStruct¨ ⍵/⍨¨~b←∧\¨∊∘'<=>'¨⍵} t/⍨ bm ⍝ ~atomTypes∊⍨⊂⍵ is for mainly checking if something is a string
  out ← {0=≢⍵:⍵⋄'>'∘,⍣¯1¨⍵} {(~atomTypes∊⍨⊂⍵)∧'='≡⊃⍵: ,'P' ⋄ ⍵}¨ {0=≢⍵: ⍵ ⋄ (b/¨⍵),¨ NameOfStruct¨ ⍵/⍨¨~b←∧\¨∊∘'<=>'¨⍵} t/⍨~bm ⍝ ~atomTypes∊⍨⊂⍵ is for mainly checking if something is a string
  F←(io bm in out name){(io bm in out name)←⍺⍺
    inArgsList ← {1≡≢in:,⊂⍵⋄⍵}⍵
    in≢⍥≢inArgsList:0 Assert⍨'Wrong amount of arguments given to ',name,': ','≢≢⍵',⍨⍕≢in
    _←push[atomTypes⍳in] {⍺∘⊢⍵}¨⍥⌽⍵
    args ← bm
    (( bm)/args) ← Pop¨in
    ((~bm)/args) ← Push¨out
    maybeNilFunc ← ⍵⍵ ⍝ I tried simplifying out this assignment but was unsuccessful. ⎕OR with niladic functions is no joke.
    i←atomTypes⍳out,in/⍨'='=⊃¨in
    r←{1≡≢⍵:⊃⍵⋄⍵} {⍵∘⊢⍬}¨pop[i]⊣{⍬≡⍵:maybeNilFunc ⋄ maybeNilFunc⍵} args
    r
  }(⎕OR'Func')
∇


∇AfterDefaultDefinitions mode
  replacements ⊢← 0 ⎕FIX pathToRaylibDotAPLN,'../parse-raylib-apl/replacements.apln.hide'

  SetTraceLogLevelRAYLIB TraceLogLevel.LOG_WARNING
  SetTargetFPSRAYLIB 60

  ⍝ Replace all names of ⍺ that are in ⍵ with the ⍵ counterpart
  ReplaceNames ← {⍵.(⍎'⍺.',⊢,'←',⊢)¨⍣(×≢a)⊢a←⊃∩/(⍺⍵).⎕NL¯3}

  :If mode∊1 3 ⍝ Automatic replacements
    replacements {r←⍵ ⋄ r←r⊣'r'⎕NS⍺} ⎕THIS
  :EndIf

  :If mode∊2 3 ⍝ Custom replacements
    customReplacements {r←⍵ ⋄ r←r⊣'r'⎕NS⍺} ⎕THIS
  :EndIf
∇
replacements ← ⎕NS''

:Namespace customReplacements
⍝ Automatic replacements are good enough currently :D

:EndNamespace


⍝ list of rgba-format colors for convenience
:Namespace color
  aliceblue            ← 240 248 255 255
  antiquewhite         ← 250 235 215 255
  aqua                 ← 0   255 255 255
  aquamarine           ← 127 255 212 255
  azure                ← 240 255 255 255
  beige                ← 211 176 131 255
  bisque               ← 255 228 196 255
  black                ← 0   0   0   255
  blanchedalmond       ← 255 235 205 255
  blue                 ← 0   121 241 255
  blueviolet           ← 138 43  226 255
  brown                ← 127 106 79  255
  burlywood            ← 222 184 135 255
  cadetblue            ← 95  158 160 255
  chartreuse           ← 127 255 0   255
  chocolate            ← 210 105 30  255
  coral                ← 255 127 80  255
  cornflowerblue       ← 100 149 237 255
  cornsilk             ← 255 248 220 255
  crimson              ← 220 20  60  255
  cyan                 ← 0   255 255 255
  darkblue             ← 0   82  172 255
  darkbrown            ← 76  63  47  255
  darkcyan             ← 0   139 139 255
  darkgoldenrod        ← 184 134 11  255
  darkGray             ← 80  80  80  255
  darkGrey             ← 80  80  80  255
  darkgreen            ← 0   117 44  255
  darkkhaki            ← 189 183 107 255
  darkmagenta          ← 139 0   139 255
  darkolivegreen       ← 85  107 47  255
  darkorange           ← 255 140 0   255
  darkorchid           ← 153 50  204 255
  darkpurple           ← 112 31  126 255
  darkred              ← 139 0   0   255
  darksalmon           ← 230 150 122 255
  darkseagreen         ← 143 188 143 255
  darkslateblue        ← 72  61  139 255
  darkslategray        ← 47  79  79  255
  darkslategrey        ← 47  79  79  255
  darkturquoise        ← 64  206 209 255
  darkviolet           ← 148 0   211 255
  deeppink             ← 255 20  147 255
  deepskyblue          ← 0   191 255 255
  dimgray              ← 105 105 105 255
  dimgrey              ← 105 105 105 255
  dodgerblue           ← 30  144 255 255
  firebrick            ← 178 34  34  255
  floralwhite          ← 255 250 240 255
  forestgreen          ← 34  139 34  255
  fuschia              ← 255 0   255 255
  gainsboro            ← 220 220 220 255
  ghostwhite           ← 248 248 255 255
  gold                 ← 255 203 0   255
  goldenrod            ← 218 165 32  255
  gray                 ← 130 130 130 255
  grey                 ← 130 130 130 255
  green                ← 0   228 48  255
  greenyellow          ← 173 255 47  255
  honeydew             ← 240 255 240 255
  hotpink              ← 255 105 180 255
  indianred            ← 205 92  92  255
  indigo               ← 75  0   130 255
  ivory                ← 255 255 240 255
  khaki                ← 240 230 140 255
  lavender             ← 230 230 250 255
  lavenderblush        ← 255 240 245 255
  lawngreen            ← 124 252 0   255
  lemonshiffon         ← 255 250 205 255
  lightblue            ← 173 216 230 255
  lightcoral           ← 240 128 128 255
  lightcyan            ← 225 255 255 255
  lightgoldenrodyellow ← 250 250 210 255
  lightGray            ← 200 200 200 255
  lightGrey            ← 200 200 200 255
  lightgreen           ← 144 238 144 255
  lightred             ← 255 102 102 255
  lightsalmon          ← 255 160 122 255
  lightseagreen        ← 32  178 170 255
  lightskyblue         ← 135 206 250 255
  lightslategray       ← 119 136 153 255
  lightslategrey       ← 119 136 153 255
  lightsteelblue       ← 176 196 222 255
  lightyellow          ← 255 255 224 255
  lime                 ← 0   158 47  255
  limegreen            ← 50  205 50  255
  linen                ← 250 240 230 255
  magenta              ← 255 0   255 255
  maroon               ← 190 33  55  255
  mediumaquamarine     ← 102 205 170 255
  mediumblue           ← 0   0   205 255
  mediumorchid         ← 186 85  211 255
  mediumpurple         ← 147 112 219 255
  mediumseagreen       ← 60  179 113 255
  mediumslateblue      ← 123 104 238 255
  mediumspringgreen    ← 0   250 154 255
  mediumturquoise      ← 72  209 204 255
  mediumvioletred      ← 199 21  133 255
  midnightblue         ← 25  25  112 255
  mintcream            ← 245 255 250 255
  mistyrose            ← 255 228 225 255
  moccasin             ← 255 228 181 255
  navajowhite          ← 255 228 181 255
  navy                 ← 0   0   128 255
  oldlace              ← 253 245 230 255
  olive                ← 128 128 0   255
  olivedrab            ← 107 142 35  255
  orange               ← 255 161 0   255
  orangered            ← 255 69  0   255
  orchid               ← 218 112 214 255
  palegoldenrod        ← 238 232 170 255
  palegreen            ← 152 251 152 255
  paleturquoise        ← 175 238 238 255
  palevioletred        ← 219 112 147 255
  papayawhip           ← 255 239 213 255
  peachpuff            ← 255 218 185 255
  peru                 ← 205 133 63  255
  pink                 ← 255 109 194 255
  plum                 ← 221 160 221 255
  powderblue           ← 176 224 230 255
  purple               ← 200 122 255 255
  raywhite             ← 245 245 245 255
  red                  ← 230 41  55  255
  rosybrown            ← 188 143 143 255
  royalblue            ← 65  105 225 255
  saddlebrown          ← 139 69  19  255
  salmon               ← 250 128 114 255
  sandybrown           ← 244 164 96  255
  seagreen             ← 46  139 87  255
  seashell             ← 255 248 238 255
  sienna               ← 160 82  45  255
  silver               ← 192 192 192 255
  skyblue              ← 102 191 255 255
  slateblue            ← 106 90  205 255
  slategray            ← 112 128 144 255
  slategrey            ← 112 128 144 255
  snow                 ← 255 250 250 255
  springgreen          ← 0   255 127 255
  steelblue            ← 70  130 180 255
  tan                  ← 210 180 140 255
  teal                 ← 0   128 128 255
  thistle              ← 216 191 216 255
  tomato               ← 255 99  71  255
  transparent          ← 0   0   0   0
  turquoise            ← 64  224 208 255
  violet               ← 135 60  190 255
  wheat                ← 245 222 179 255
  white                ← 255 255 255 255
  whitesmoke           ← 245 245 245 255
  yellow               ← 253 249 0   255
  yellowgreen          ← 154 205 50  255
:EndNamespace


:EndNamespace
